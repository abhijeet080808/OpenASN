
-- LOCAL ADDITION
-- This production is not present in spec and is added locally to facilicate
-- parsing. Eg Absent, Application.

-- LOCAL MODIFICATION
-- Converted ASN keywords like BIT and STRING to string data type to facilicate
-- parsing.

-- ORDERING CRITICAL
-- Ordering is critical here and should be exactly as shown here.
-- The longer production group with comma is to be tried first.
-- Also within that production group NamedType is to be tried first to
-- avoid going into a infinite recursion. Eg AlternativeTypeList.

-- LOCAL ADDITION
Absent ::= "ABSENT"

AdditionalElementSetSpec ::= ElementSetSpec

-- ORDERING CRITICAL
AlternativeTypeList ::=
    NamedType "," AlternativeTypeList
  | NamedType

AlternativeTypeLists ::= RootAlternativeTypeList

-- LOCAL ADDITION
Application ::= "APPLICATION"

-- LOCAL ADDITION
AsnTrue ::= "TRUE"

-- LOCAL ADDITION
AsnFalse ::= "FALSE"

ArcIdentifier ::=
    integerUnicodeLabel
  | non-integerUnicodeLabel

Assignment ::= TypeAssignment

-- ORDERING CRITICAL
AssignmentList ::=
    Assignment AssignmentList
  | Assignment

-- ORDERING CRITICAL
-- LOCAL MODIFICATION
BitStringType ::=
    "BIT" "STRING" "{" NamedBitList "}"
  | "BIT" "STRING"

-- ORDERING CRITICAL
-- LOCAL MODIFICATION
BitStringValue ::=
    bstring
  | hstring
  | "{" IdentifierList "}"
  | "{" "}"
  | "CONTAINING" Value

-- LOCAL ADDITION
BMPString ::= "BMPString"

BooleanType ::= "BOOLEAN"

BooleanValue ::=
    AsnTrue
  | AsnFalse

BuiltinType ::=
    BitStringType
  | BooleanType
  | CharacterStringType
  | ChoiceType
  | DateType
  | DateTimeType
  | DurationType
  | EmbeddedPDVType
  | EnumeratedType
  | ExternalType
  --| InstanceOfType
  | IntegerType
  | IRIType
  | NullType
  --| ObjectClassFieldType
  | ObjectIdentifierType
  | OctetStringType
  | RealType
  | RelativeIRIType
  | RelativeOIDType
  | SequenceType
  | SequenceOfType
  | SetType
  | SetOfType
  | PrefixedType
  | TimeType
  | TimeOfDayType

BuiltinValue ::=
    BitStringValue
  | BooleanValue
-- CharacterStringValue->UnrestrictedCharacterStringValue is also
-- equivalent to EmbeddedPDVValue and ExternalValue
  | CharacterStringValue
  | ChoiceValue
  --| EmbeddedPDVValue
  | EnumeratedValue
  --| ExternalValue
  --| InstanceOfValue
  | IntegerValue
  | IRIValue
  | NullValue
  --| ObjectIdentifierValue
  --| OctetStringValue
  --| RealValue
  --| RelativeIRIValue
  --| RelativeOIDValue
  --| SequenceValue
  --| SequenceOfValue
  --| SetValue
  --| SetOfValue
  --| PrefixedValue
  --| TimeValue

Cell ::= number

CharsDefn ::=
    cstring
  | Quadruple
  | Tuple
  | DefinedValue

-- ORDERING CRITICAL
CharSyms ::=
    CharsDefn "," CharSyms
  | CharsDefn

CharacterStringList ::= "{" CharSyms "}"

CharacterStringType ::=
    RestrictedCharacterStringType
  | UnrestrictedCharacterStringType

CharacterStringValue ::=
    RestrictedCharacterStringValue
  | UnrestrictedCharacterStringValue

-- LOCAL MODIFICATION
ChoiceType ::= "CHOICE" "{" AlternativeTypeLists "}"

ChoiceValue ::= identifier ":" Value

Class ::=
    UNIVERSAL
  | APPLICATION
  | PRIVATE
  | empty

ClassNumber ::=
    number
  | DefinedValue

ComponentConstraint ::= ValueConstraint PresenceConstraint

-- LOCAL MODIFICATION
-- ORDERING CRITICAL
ComponentType ::=
    NamedType "DEFAULT" Value
  | NamedType OPTIONAL
  | NamedType
  | "COMPONENTS" "OF" Type

-- ORDERING CRITICAL
ComponentTypeList ::=
    ComponentType "," ComponentTypeList
  | ComponentType

ComponentTypeLists ::= RootComponentTypeList

-- ORDERING CRITICAL
ComponentValueList ::=
    NamedValue "," ComponentValueList
  | NamedValue

Constraint ::= "(" ConstraintSpec ExceptionSpec ")"

-- ORDERING CRITICAL
ConstraintList ::=
    Constraint ConstraintList
  | Constraint

ConstraintSpec ::= SubtypeConstraint

ContainedSubtype ::= Includes Type

-- LOCAL MODIFICATION
DateTimeType ::= "DATE-TIME"

-- LOCAL MODIFICATION
DateType ::= "DATE"

-- LOCAL MODIFICATION
DefinedType ::=
    ExternalTypeReference
  | TypeReference

-- LOCAL MODIFICATION
DefinedValue ::=
    ExternalValueReference
  | ValueReference

DefinitiveIdentification ::=
    DefinitiveOID
  | empty

DefinitiveObjIdComponent ::= NameForm

DefinitiveObjIdComponentList ::=
    DefinitiveObjIdComponent DefinitiveObjIdComponentList
  | DefinitiveObjIdComponent

DefinitiveOID ::= "{" DefinitiveObjIdComponentList "}"

-- LOCAL MODIFICATION
DurationType ::= "DURATION"

Elements ::=
    SubtypeElements
  | "(" ElementSetSpec ")"

-- LOCAL MODIFICATION
ElementSetSpec ::=
    Unions
  | "ALL" Exclusions

-- ORDERING CRITICAL
ElementSetSpecs ::=
    RootElementSetSpec "," "..." "," AdditionalElementSetSpec
  | RootElementSetSpec "," "..."
  | RootElementSetSpec

Elems ::= Elements

-- LOCAL ADDITION
Ellipsis ::= "..."

-- LOCAL MODIFICATION
EmbeddedPDVType ::= "EMBEDDED" "PDV"

EncodingPrefix ::= "[" EncodingReference "]"

EncodingPrefixedType ::= EncodingPrefix Type

-- LOCAL MODIFICATION
EncodingReference ::=
    encodingReferencePrime ":"
  | empty

-- LOCAL MODIFICATION
EnumeratedType ::= "ENUMERATED" "{" Enumerations "}"

EnumeratedValue ::= identifier

-- ORDERING CRITICAL
Enumeration ::=
    EnumerationItem "," Enumeration
  | EnumerationItem

-- ORDERING CRITICAL
EnumerationItem ::= NamedNumber | identifier

Enumerations ::= RootEnumeration

ExceptionIdentification ::=
    SignedNumber
  | DefinedValue
  | Type ":" Value

ExceptionSpec ::= "!" ExceptionIdentification | empty

-- LOCAL MODIFICATION
Exclusions ::= "EXCEPT" Elements

-- LOCAL ADDITION
Explicit ::= "EXPLICIT"

-- LOCAL MODIFICATION
ExternalType ::= "EXTERNAL"

-- LOCAL MODIFICATION
ExternalTypeReference ::= moduleReference "." typeReference

-- LOCAL MODIFICATION
ExternalValueReference ::= moduleReference "." valueReference

FirstArcIdentifier ::= "/" ArcIdentifier

FullSpecification ::= "{" TypeConstraints "}"

-- LOCAL ADDITION
GeneralizedTime ::= "GeneralizedTime"

-- LOCAL ADDITION
GeneralString ::= "GeneralString"

-- LOCAL ADDITION
GraphicString ::= "GraphicString"

Group ::= number

-- LOCAL ADDITION
IA5String ::= "IA5String"

-- ORDERING CRITICAL
IdentifierList ::=
    identifier "," IdentifierList
  | identifier

IElems ::= Intersections

-- LOCAL ADDITION
Implicit ::= "IMPLICIT"

-- LOCAL MODIFICATION
Includes ::= "INCLUDES" | empty

-- LOCAL MODIFICATION
InnerTypeConstraints ::=
    "WITH" "COMPONENT" SingleTypeConstraint
  | "WITH" "COMPONENTS" MultipleTypeConstraints

-- LOCAL MODIFICATION
-- ORDERING CRITICAL
IntegerType ::=
    "INTEGER" "{" NamedNumberList "}"
  | "INTEGER"

-- ORDERING CRITICAL
IntegerValue ::= SignedNumber | identifier

-- ORDERING CRITICAL
IntersectionElements ::=
    Elems Exclusions
  | Elements

-- LOCAL MODIFICATION
IntersectionMark ::= "^" | "INTERSECTION"

--Intersections ::=
--    IElems IntersectionMark IntersectionElements
--  | IntersectionElements

-- LOCAL MODIFICATION
IRIType ::= "OID-IRI"

IRIValue ::= """ FirstArcIdentifier SubsequentArcIdentifier """

-- LOCAL MODIFICATION
ISO646String ::= "ISO646String"

-- LOCAL ADDITION
LessThan ::= "<"

-- LOCAL MODIFICATION
LowerEndpoint ::= LowerEndValue | LowerEndValue LessThan

-- LOCAL MODIFICATION
LowerEndValue ::= Value | Min

-- LOCAL ADDITION
Max ::= "MAX"

-- LOCAL ADDITION
Min ::= "MIN"

ModuleBody ::=
    AssignmentList
  | empty

-- LOCAL MODIFICATION
ModuleDefinition ::=
  ModuleIdentifier
  "DEFINITIONS"
  --EncodingReferenceDefault
  --TagDefault
  --ExtensionDefault
  "::="
  "BEGIN"
  ModuleBody
  --EncodingControlSections
  "END"

ModuleIdentifier ::= ModuleReference DefinitiveIdentification

MultipleTypeConstraints ::= FullSpecification | PartialSpecification

NamedBit ::=
    identifier "(" number ")"
  | identifier "(" DefinedValue ")"

-- ORDERING CRITICAL
NamedBitList ::=
    NamedBit "," NamedBitList
  | NamedBit

NamedConstraint ::= identifier ComponentConstraint

NamedNumber ::=
    identifier "(" SignedNumber ")"
  | identifier "(" DefinedValue ")"

-- ORDERING CRITICAL
NamedNumberList ::=
    NamedNumber "," NamedNumberList
  | NamedNumber

NamedType ::= identifier Type

NamedValue ::= identifier Value

NameForm ::= identifier

-- LOCAL MODIFICATION
NullType ::= "NULL"

-- LOCAL MODIFICATION
NullValue ::= "NULL"

-- LOCAL ADDITION
NumericString ::= "NumericString"

-- LOCAL ADDITION
ObjectDescriptor ::= "ObjectDescriptor"

-- LOCAL MODIFICATION
ObjectIdentifierType ::= "OBJECT" "IDENTIFIER"

-- ORDERING CRITICAL
AlternativeTypeList ::=
    NamedType "," AlternativeTypeList
  | NamedType

-- LOCAL MODIFICATION
OctetStringType ::= "OCTET" "STRING"

-- LOCAL ADDITION
Optional ::= "OPTIONAL"

PartialSpecification ::= "{" "..." "," TypeConstraints "}"

-- LOCAL MODIFICATION
PatternConstraint ::= "PATTERN" Value

-- LOCAL MODIFICATION
PermittedAlphabet ::= "FROM" Constraint

Plane ::= number

PrefixedType ::=
    TaggedType
  | EncodingPrefixedType

-- LOCAL MODIFICATION
PresenceConstraint ::= Present | Absent | Optional | empty

-- LOCAL ADDITION
Present ::= "PRESENT"

-- LOCAL ADDITION
PrintableString ::= "PrintableString"

-- LOCAL ADDITION
Private ::= "PRIVATE"

PropertyAndSettingPair ::= PropertyName "=" SettingName

-- LOCAL MODIFICATION
PropertyName ::= PsName

-- LOCAL MODIFICATION
-- See 51.10.1
PropertySettings ::= "SETTINGS" """ PropertySettingsList """

-- ORDERING CRITICAL
PropertySettingsList ::=
    PropertyAndSettingPair PropertySettingsList
  | PropertyAndSettingPair

Quadruple ::= "{" Group "," Plane "," Row "," Cell "}"

-- LOCAL MODIFICATION
RealType ::= "REAL"

ReferencedType ::=
    DefinedType
  | UsefulType
  | SelectionType
  --| TypeFromObject
  --| ValueSetFromObjects

-- LOCAL MODIFICATION
RelativeIRIType ::= "RELATIVE-OID-IRI"

-- LOCAL MODIFICATION
RelativeOIDType ::= "RELATIVE-OID"

RestrictedCharacterStringType ::=
    BMPString
  | GeneralString
  | GraphicString
  | IA5String
  | ISO646String
  | NumericString
  | PrintableString
  | TeletexString
  | T61String
  | UniversalString
  | UTF8String
  | VideotexString
  | VisibleString

RestrictedCharacterStringValue ::=
    cstring
  | CharacterStringList
  | Quadruple
  | Tuple

RootAlternativeTypeList ::= AlternativeTypeList

RootComponentTypeList ::= ComponentTypeList

RootElementSetSpec ::= ElementSetSpec

RootEnumeration ::= Enumeration

Row ::= number

SelectionType ::= identifier "<" Type

SequenceOfType ::= "SEQUENCE" "OF" Type | "SEQUENCE" "OF" NamedType
















ValueRange ::= LowerEndpoint ".." UpperEndpoint
